## ams_version=1.0

Model Main_Distribution_Flow {
	Section ExcelImport {
		StringParameter sp_workbookName;
		Procedure pr_readExcel {
			Body: {
				if not FileSelect(sp_workbookName, extension:".xlsx") then
					return;
				endif;
				
				axll::CloseAllWorkBooks;
				axll::OpenWorkBook(sp_workbookName);
				
				axll::SelectSheet("coordinates");
				
				axll::ReadList(p_latitude, "A2:A7323", "C2:C7323", ModeForUnknownElements : 1);
				axll::ReadList(p_longitude, "A2:A7323", "D2:D7323", ModeForUnknownElements : 1);
				axll::ReadList(p_population, "A2:A7323", "E2:E7323", ModeForUnknownElements : 1);
				
				axll::CloseAllWorkBooks;
			}
		}
		Procedure pr_filterCities {
			Body: {
				for (l in s_locations | p_population(l) > 300000 and 
					p_latitude(l) > 40 and p_latitude(l) < 70 and 
					p_longitude(l) > -20 and p_longitude(l) < 20) do
						s_locationsFiltered += l;
				endfor;
				
				s_locations := s_locationsFiltered;
			}
			Set s_locationsFiltered {
				SubsetOf: s_locations;
			}
		}
	}
	Section DatabaseImport {
		DatabaseTable db_coordinates {
			DataSource: {
				"Driver=MySQL ODBC 8.0 Unicode Driver;Server=127.0.0.1;Port=3306;Database=world;User=root;Password=root;NO_SCHEMA=false;NO_CATALOG=true"
			}
			TableName: "coordinates";
			Owner: "world";
			Mapping: {
				"city" -->l,
				"lat"  -->p_latitude,
				"lng"  -->p_longitude,
				"pop"  -->p_population
			}
		}
		DatabaseProcedure dbpr_query {
			DataSource: {
				"Driver=MySQL ODBC 8.0 Unicode Driver;Server=127.0.0.1;Port=3306;Database=world;User=root;Password=root;NO_SCHEMA=false;NO_CATALOG=true"
			}
			SqlQuery: "SELECT * FROM `coordinates` WHERE (pop > 300000 AND lat > 40 AND lat < 70 AND lng > -20 AND lng < 20)";
			Owner: "world";
			Property: UseResultSet;
			Mapping: {
				"city" -->l,
				"lat"  -->p_latitude,
				"lng"  -->p_longitude,
				"pop"  -->p_population
			}
		}
		Procedure pr_readTable {
			Body: {
				! read from table db_coordinates;
				read from table dbpr_query;
			}
		}
	}
	Section ImportData {
		Set s_locations {
			Index: l, m;
			webui::AnnotationsIdentifier: sp_locationType;
		}
		Set s_locationTypes {
			Index: typ;
			Definition: {
				{'RDC', 'NDC', 'Factory'}
			}
		}
		Set s_time {
			Index: t;
			Definition: {
				{'Monday', 'Tuesday', 'Wednesday', 'Thursday', 'Friday'}
			}
		}
		Parameter bp_locationType {
			IndexDomain: (l, typ);
			Range: binary;
		}
		Parameter p_distance {
			IndexDomain: (l, m);
		}
		Parameter p_demand {
			IndexDomain: (l, t);
		}
		Parameter p_supply {
			IndexDomain: l;
		}
		Parameter p_minStock {
			IndexDomain: l;
		}
		Parameter p_maxStock {
			IndexDomain: l;
		}
		Parameter p_startStock {
			IndexDomain: l;
		}
		Parameter p_transportCost {
			IndexDomain: (l, m);
			Definition: {
				if bp_locationType(l, 'Factory') and bp_locationType(m, 'NDC') then ! primary flow
					0.8
				elseif bp_locationType(l, 'NDC') and bp_locationType(m, 'RDC') then ! secondary flow
					1.2
				! else
				!	 0
				endif
			}
		}
		Parameter p_longitude {
			IndexDomain: l;
		}
		Parameter p_latitude {
			IndexDomain: l;
		}
		Parameter p_population {
			IndexDomain: l;
		}
		Procedure pr_chooseLocationType {
			Body: {
				! the rest of the locations should be all RDCs
				bp_locationType(l, 'RDC') := 1;
				
				Option seed = 42; ! fix pseudo-random results
				
				s_otherLocations := s_locations;
				
				! choose randomly 5 factories
				while sum(l, bp_locationType(l, 'Factory')) < 5 do
				      ep_location := Element( 
				      	s_otherLocations, 
				      	round( uniform( 0.5, card(s_otherLocations)+0.5 ) )
				      );
				      bp_locationType(ep_location, 'Factory') := 1;
				      bp_locationType(ep_location, 'RDC') := 0;
				      s_otherLocations -= ep_location;
				endwhile;
				
				! choose randomly 3 NDCs
				while sum(l, bp_locationType(l, 'NDC')) < 3 do
				      ep_location := Element(
				      	s_otherLocations, 
				      	round( uniform( 0.5, card(s_otherLocations)+0.5) )
				      );
				      bp_locationType(ep_location, 'NDC') := 1;
				      s_otherLocations -= ep_location;
				endwhile;
			}
			Set s_otherLocations {
				SubsetOf: s_locations;
			}
			ElementParameter ep_location {
				Range: s_locations;
			}
		}
		Procedure pr_generateData {
			Body: {
				p_scale := 1 / 1000;
				
				! demand is proportional to the population, but with a small random noise
				p_demand(l, t) := round( p_population(l) * (1 + uniform(-0.1, 0.1)) * p_scale );
				! supply should always meet our previously generated demand to not produce a infeasible model (balanced network model)
				p_supply(l) | bp_locationType(l, 'Factory') := max( t, sum(m, p_demand(m, t)) ) / sum(m, bp_locationType(m, 'Factory'));
				! distance between two points on a sphere can be computed by the Haversine formula
				p_distance(l, m) := 
					sqrt( ((p_latitude(l)-p_latitude(m)) * 111.320 * cos(radians(1/2 * (p_latitude(l)+p_latitude(m)))))^2 + ((p_latitude(l)-p_latitude(m)) * 110.574 )^2 ) * 1;
			}
			Parameter p_scale;
		}
	}
	Section MathModel {
		Parameter bp_isArcAllowed {
			IndexDomain: (l, m);
			Range: binary;
			Definition: {
				bp_locationType(l, 'Factory') and bp_locationType(m, 'NDC') ! primary flow
				or
				bp_locationType(l, 'NDC') and bp_locationType(m, 'RDC')     ! secondary flow
			}
		}
		Variable v_transportation {
			IndexDomain: (l, m, t) | bp_isArcAllowed(l, m);
			Range: nonnegative;
		}
		Variable v_production {
			IndexDomain: (l, t) | bp_locationType(l, 'Factory');
			Range: nonnegative;
		}
		Constraint c_supply {
			IndexDomain: (l, t);
			Definition: v_production(l, t) <= p_supply(l);
		}
		Constraint c_nodeEquilibrium {
			IndexDomain: (l, t);
			Definition: {
				! input = output
				v_production(l, t) + sum(m, v_transportation(m, l, t)) = sum(m, v_transportation(l, m, t)) + p_demand(l, t)
			}
		}
		Variable v_objectiveFunction {
			Range: free;
			Definition: sum((l, m, t), v_transportation(l, m, t) * p_distance(l, m) * p_transportCost(l, m));
		}
		MathematicalProgram mp_minCost {
			Objective: v_objectiveFunction;
			Direction: minimize;
			Constraints: AllConstraints;
			Variables: AllVariables;
			Type: Automatic;
		}
	}
	Section WebUISupport {
		StringParameter sp_primaryAction {
			IndexDomain: webui::indexPageActionSpec;
			Definition: {
				if webui::indexPageActionSpec = 'displaytext' then "Solve"
				elseif webui::indexPageActionSpec = 'icon' then "aimms-paperplane"
				elseif webui::indexPageActionSpec = 'procedure' then "MainExecution"
				elseif webui::indexPageActionSpec = 'state' then "Active"
				endif
			}
		}
		StringParameter sp_locationType {
			IndexDomain: l;
			Definition: {
				if bp_locationType(l, 'Factory') then
					"Factory"
				elseif bp_locationType(l, 'NDC') then
					"NDC"
				elseif bp_locationType(l, 'RDC') then
					"RDC"
				endif
			}
		}
		Parameter p_averageTransport {
			IndexDomain: (l, m) | bp_isArcAllowed(l, m);
			Definition: mean(t, v_transportation(l, m, t));
		}
	}
	Procedure MainInitialization {
		Comment: "Add initialization statements here that do NOT require any library being initialized already.";
	}
	Procedure PostMainInitialization {
		Body: {
			! pr_readExcel;
			! pr_filterCities;
			
			pr_readTable;
			
			pr_chooseLocationType;
			pr_generateData;
		}
		Comment: {
			"Add initialization statements here that require that the libraries are already initialized properly,
			or add statements that require the Data Management module to be initialized."
		}
	}
	Procedure MainExecution {
		Body: {
			solve mp_minCost;
		}
	}
	Procedure PreMainTermination {
		Body: {
			return DataManagementExit();
		}
		Comment: {
			"Add termination statements here that require all libraries to be still alive.
			Return 1 if you allow the termination sequence to continue.
			Return 0 if you want to cancel the termination sequence."
		}
	}
	Procedure MainTermination {
		Body: {
			return 1;
		}
		Comment: {
			"Add termination statements here that do not require all libraries to be still alive.
			Return 1 to allow the termination sequence to continue.
			Return 0 if you want to cancel the termination sequence.
			It is recommended to only use the procedure PreMainTermination to cancel the termination sequence and let this procedure always return 1."
		}
	}
	Section Quantities_and_Units {
		Comment: {
			"This section contains all the units and quantities that are added automatically by AIMMS.
			It is recommended to declare all your quantities here."
		}
		Quantity SI_Time_Duration {
			BaseUnit: s;
			Conversions: minute->s : #-># * 60;
			Comment: {
				"Expresses the value for the duration of periods.
				
				The unit s has been added automatically because it is a required unit for AimmsWebUI.
				
				The unit minute has been added automatically because it is a required unit for AimmsWebUI."
			}
		}
	}
}
